body file control
{
  namespace => "nickanderson";
}
bundle agent inventory_clamav
# @brief Inventory ClamAV
# @inventory ClamAV version
# @inventory ClamAV definitions version
# @inventory ClamAV definitions date
# @variable nickanderson:inventory_clamav.definitions_date_format_string Format to apply to date string, e.g. %Y-%m-%d
{
  vars:

      # As of 2022-10-05 date was not part of paths in stdlib, so we try to use
      # it if it does become available and fall back

      "_bin_date"
        string => ifelse( isvariable( "default:paths.date" ), "$(default:paths.date)",
                          "/usr/bin/date" ),
        if => isexecutable( "/usr/bin/date" );


      "_bin_clamscan"
        string => "/usr/bin/clamscan";

      # Example clamscan ---version output: ClamAV 0.102.2/25803/Tue May 5 12:19:25 2020
      "_clamscan_version_data"
        data => data_regextract( "\w+(?<binary_version>.*)/(?<definitions_version>\d+)/(?<definitions_date>.*)",
                                 execresult( "$(_bin_clamscan) --version", noshell)),
        if => isexecutable( $(_bin_clamscan) );

      "binary_version"
        string => "$(_clamscan_version_data[binary_version])",
        meta => { "inventory", "attribute_name=ClamAV version" },
        if => isvariable( "_clamscan_version_data" );

      "definitions_version"
        string => "$(_clamscan_version_data[definitions_version])",
        meta => { "inventory", "attribute_name=ClamAV definitions version" },
        if => isvariable( "_clamscan_version_data" );


      # Inventory the raw date if definitions_date_format_string is not specified.
      "definitions_date"
        string => "$(_clamscan_version_data[definitions_date])",
        meta => { "inventory", "attribute_name=ClamAV definitions date" },
        unless => isvariable( definitions_date_format_string );

      # Inventory the formatted date if definitions_date_format_string is specified.
      "definitions_date"
        string => "$(with)",
        meta => { "inventory", "attribute_name=ClamAV definitions date" },
        with => execresult( concat( '$(_bin_date) --date="$(_clamscan_version_data[definitions_date])" ',
                                    '"+$(definitions_date_format_string)"'), useshell),
        if => isvariable( definitions_date_format_string );
}
body file control
{
        namespace => "default";
}
bundle agent __main__
{
      methods: "nickanderson:inventory_clamav";
}
